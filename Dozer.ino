#include <TimerMs.h>  //библ таймера
#include <EncButton.h> //библ энкодер-кнопка
#include <LiquidCrystal_I2C.h> //библ LCD i2c

//объявл. констант
#define dtmin 100    // dtmax, dtmin - диапазон разрешенных значений времени, все в мсек
#define dtmax 4000   //время макс — для отладки 1 сек, фактически бывает до 4 секунд
#define pauz 200     //принуд. пауза после налива, защита от случ. нажатия в мсек
#define valvepin 12  //назначен вывод клапана

//объявл. переменных
int dt = 350; // dt — заданное изначально время сработки таймера
uint8_t prd_count = 0; // счетчик интервалов для округления 
uint8_t stp = 25;  //stp инкремент в мсек на шаг энкодера

LiquidCrystal_I2C lcd(0x27, 16, 2);  // LCD — адрес , столбцов, строк
TimerMs doz(dt, 0, 1); //инициализация таймера dt - уст время, 0 остановлен, 1 - режим "таймер" // (период, мсек ), (0 не запущен / 1 запущен), (режим: 0 период / 1 таймер)
TimerMs prd(stp, 0, 0);  //таймер интервалов кратных инкременту, stp - шаг, 0 остановлен, 0 режим период
EncButton eb(2, 3, 4); // энкодер на 2-3-4 пины
Button btn5(5); // кнопка "авто старт" на 5 пин

void setup() {
  doz.setTimerMode(); //режим однократный обратный отсчет
  doz.stop(); // принудтельно стоп таймер
  pinMode(valvepin, OUTPUT);  //настройка пина клапан на выход
  digitalWrite(valvepin, LOW); //сброс valvepin пина в 0
  lcd.init();           // инициализация Lcd
  lcd.clear(); //очистка дисплея
  lcd.backlight();      // включить подсветку lcd
  lcd.home();  // курсор в исх положение 
  lcd.print(String("Time: ") + dt + " msec ");
}

void loop() {
   eb.tick(); //опрос энкодера
   btn5.tick(); //опрос кнопки 5 авто

      //обработка поворота энкодера
    if (eb.turn()) {
       time_increment();
       }
    if (btn5.press()) { //опрос кнопки 
       fill_timer(); //вызов подпрограммы наполнения "полуавтомат"
       }
}

// подпрограмма настройки времени энкодером
void time_increment() {
        stp = (dt > 975) ? 100 : 25; //шаг инкремента зависит от значения времени сработки
        dt += stp * eb.dir(); //изменяем период на инкремент со знаком направления вращения энкодера
        dt = (dt < dtmin) ? (dtmin) : dt; // условие меньше меньшего
        dt = (dt > dtmax) ? (dtmax) : dt; // условие больше большего
        lcd.setCursor(6, 0);  // курсор на лсд столбец 6 строка 0 
        lcd.print(String(dt) + " msec "); // вывод на лсд текущего периода после всех проверок
}

//подпрограмма  "полуавтомат"
void fill_timer() {
    doz.setTime(dt); //передаем в таймер текущее значение периода
    doz.start(); //запуск отсчета
    digitalWrite(valvepin, HIGH); //вкл клапан
    while (doz.status() == true); //крутимся на месте ждем флаг окончания таймера
    digitalWrite(valvepin, LOW); // выкл клапан
    doz.setTime(pauz); // пауза pauz мсек от быстрого повторного нажатия кнопки 
    doz.start(); // запуск таймера на pauz мсек
    while (doz.status() == true); //крутимся на месте ждем флаг окончания таймера
}
